wf := import("@platforma-sdk/workflow-tengo:workflow")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
pframes := import("@platforma-sdk/workflow-tengo:pframes")

pfClusterMarkersConv := import(":pf-cluster-markers-conv")
pfDEGConversion := import(":pf-DEG-conv")

wf.prepare(func(args){
	return {
		resolvedInput: wf.resolve(args.countsRef, { errIfMissing: true }),
		resolvedClusterAnnotation: wf.resolve(args.clusterAnnotationRef, { errIfMissing: true })
	}
})

wf.body(func(args) {

	blockId := wf.blockId().getDataAsJson()
	rawCounts := args.resolvedInput
	inputSpec := rawCounts.spec
	species := inputSpec.domain["pl7.app/species"]
	topN := args.topN
	strictOverlap := args.strictOverlap
	logfcCutoff := args.logfcCutoff
	pvalCutoff := args.pvalCutoff

	clusterAnnotation := args.resolvedClusterAnnotation
	clusterColumn := clusterAnnotation.spec.annotations["pl7.app/label"]
	
	csvClusters := xsv.exportFrame([clusterAnnotation], "csv", {})

	csvCounts := xsv.exportFrame([rawCounts], "csv", {})

	clusterMarkers := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.cluster-markers.software:calculate-clusterMarkers")).
		addFile("rawCounts.csv", csvCounts).
		addFile("clusters.csv", csvClusters).
		arg("--counts").arg("rawCounts.csv").
		arg("--clusters").arg("clusters.csv").
		arg("--cluster_column").arg(string(clusterColumn)).
		arg("--top_n").arg(string(topN)).
		arg("--logfc_cutoff").arg(string(logfcCutoff)).
		arg("--pval_cutoff").arg(string(pvalCutoff))

	if strictOverlap {
		clusterMarkers = clusterMarkers.arg("--so")
	}

	clusterMarkers = clusterMarkers.
		// arg("--species").arg(string(species)).
		saveFile("cluster_markers.csv").
		saveFile("top_markers.csv").
		saveFile("DEG.csv").
		printErrStreamToStdout().
		saveStdoutContent().
		cache(24 * 60 * 60 * 1000).
		run()

	clusterMarkersImportParams := pfClusterMarkersConv.getColumns(blockId, species)
	clusterMarkersPf := xsv.importFile(clusterMarkers.getFile("cluster_markers.csv"), "csv", clusterMarkersImportParams)
	clusterMarkersTopPf := xsv.importFile(clusterMarkers.getFile("top_markers.csv"), "csv", clusterMarkersImportParams)

	// Construct degPF for export for functional analysis
	DEGImportParams := pfDEGConversion.getColumns(blockId, species)
	DEGPf := xsv.importFile(clusterMarkers.getFile("DEG.csv"), "csv", DEGImportParams)

	exports := {
        DEG: {
            spec: DEGPf["log2foldchange.spec"],
            data: DEGPf["log2foldchange.data"]
        },
        regDir: {
            spec: DEGPf["regulationDirection.spec"],
            data: DEGPf["regulationDirection.data"]
        }
    }

	return {
		outputs: {
			clusterMarkersPf: pframes.exportFrame(clusterMarkersPf),
			clusterMarkersTopPf: pframes.exportFrame(clusterMarkersTopPf)
		},
		exports: exports
	}
})

